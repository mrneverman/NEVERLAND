module "nevertown" {
  source                = "./modules/gci"
  instance_name         = "nevertown"
  ssh_user              = var.ssh_user
  bastion_ip            = google_compute_instance.port-of-neverland.network_interface.0.access_config.0.nat_ip
  instance_ansible_file = "../ansible/nevertown_init.yaml"
  tags                  = ["k8s-master", "internal"]
  subnetwork_name       = google_compute_subnetwork.neverland-gcn-subnetwork.id
}

module "worktowns" {
  source                = "./modules/gci"
  instance_name         = each.key
  ssh_user              = var.ssh_user
  bastion_ip            = google_compute_instance.port-of-neverland.network_interface.0.access_config.0.nat_ip
  instance_ansible_file = "../ansible/worktown_init.yaml"
  tags                  = ["k8s-worker", "internal"]
  subnetwork_name       = google_compute_subnetwork.neverland-gcn-subnetwork.id
  for_each              = toset(var.worker_nodes_name)
}

output "nevertown-IP" {
  value = module.nevertown.ip
}


output "worktowns-IPs" {
  value = {
    for name in var.worker_nodes_name : name => module.worktowns[name].ip
  }

}

/* Ansible variables */
resource "local_file" "tf_k8node_vars_file_new" {
  content  = <<-DOC
    # Ansible vars_file containing variable values from Terraform.
    # Generated by Terraform mgmt configuration.
    nevertown_internal_ip: ${module.nevertown.ip}
    DOC
  filename = "../ansible/terraform_variables/tf_k8node_vars_file.yml"
}

